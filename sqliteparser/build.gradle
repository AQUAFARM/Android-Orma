// SQLiteParser managed by by Antlr: https://github.com/antlr/antlr4

apply plugin: 'java'

final PACKAGE = 'com.github.gfx.android.orma.sqliteparser.g'
final PACKAGE_PATH = PACKAGE.replaceAll(/\./, '/')
final GRAMMAR_FILE = project.file("src/main/antlr/$PACKAGE_PATH/SQLite.g4")

final ANTLR_OUTPUT_DIR = "$buildDir/generated/source/antlr"

configurations {
    antlr
}

sourceSets.main.java.srcDir ANTLR_OUTPUT_DIR

task antlr(type: JavaExec) {
    main = 'org.antlr.v4.Tool'
    args = [GRAMMAR_FILE, '-o', "$ANTLR_OUTPUT_DIR/$PACKAGE_PATH", '-package', PACKAGE, '-Werror', '-long-messages']
    classpath =  configurations.antlr
}

compileJava.dependsOn antlr

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    antlr 'org.antlr:antlr4:4.5.1'
    compile 'org.antlr:antlr4-runtime:4.5.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
